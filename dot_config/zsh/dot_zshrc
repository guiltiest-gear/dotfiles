#!/bin/zsh
# If not running interactively, don't do anything
[[ $- != *i* ]] && return

# Remove annoying zinit aliases
declare -A ZINIT=([NO_ALIASES]=1)

# Set up zinit
ZINIT_HOME="${XDG_DATA_HOME:-${HOME}/.local/share}/zinit/zinit.git"
[ ! -d $ZINIT_HOME ] && mkdir -p "$(dirname $ZINIT_HOME)"
[ ! -d $ZINIT_HOME/.git ] && git clone https://github.com/zdharma-continuum/zinit.git "$ZINIT_HOME"
source "${ZINIT_HOME}/zinit.zsh"

# Edit line in vim with ctrl-e
autoload -Uz edit-command-line
zle -N edit-command-line
bindkey '^E' edit-command-line

# Trap USR1 signals to trigger a rehash
TRAPUSR1() {
  rehash
}

# Use fd to list path candidates
_fzf_compgen_path() {
  fd --hidden --follow --exclude ".git" . "$1"
}

# Use fd to generate the list for directory completion
_fzf_compgen_dir() {
  fd --type d --hidden --follow --exclude ".git" . "$1"
}

# Enable colors and change prompt:
autoload -U colors && colors

# vi mode
bindkey -v
# export KEYTIMEOUT=1

# Exports
export ABBR_QUIETER=1
export FZF_DEFAULT_OPTS="--no-mouse --cycle --tiebreak=chunk"
export FZF_DEFAULT_COMMAND='rg --files --hidden --smart-case --glob "!.git/*"'
export ZSH_AUTOSUGGEST_STRATEGY=(completion history)
_ZO_EXCLUDE_DIRS=$(eval 'echo /tmp{,/\*} $XDG_DATA_HOME/{nvim,chezmoi{,/\*}} \
$XDG_STATE_HOME/nvim{,/\*} \
$HOME{,/{Downloads,Desktop,Music,Documents,Pictures,Videos}{,/\*}} \
| tr " " ":"')
export _ZO_EXCLUDE_DIRS
export _ZO_FZF_OPTS="--scheme=path \
  --highlight-line \
  --info=inline-right \
  --ansi \
  --layout=reverse \
  --border=none
  --color=bg+:#283457 \
  --color=bg:#16161e \
  --color=border:#27a1b9 \
  --color=fg:#c0caf5 \
  --color=gutter:#16161e \
  --color=header:#ff9e64 \
  --color=hl+:#2ac3de \
  --color=hl:#2ac3de \
  --color=info:#545c7e \
  --color=marker:#ff007c \
  --color=pointer:#ff007c \
  --color=prompt:#2ac3de \
  --color=query:#c0caf5:regular \
  --color=scrollbar:#27a1b9 \
  --color=separator:#ff9e64 \
  --color=spinner:#ff007c
"
export YSU_MESSAGE_POSITION="after"
export STARSHIP_CONFIG="$XDG_CONFIG_HOME"/starship/starship.toml
export ZSH_EVALCACHE_DIR="$XDG_CACHE_HOME"/zsh/evalcache

# Various options
setopt aliases
setopt complete_aliases
setopt glob_dots
setopt interactive_comments
setopt append_history
setopt hist_ignore_dups
setopt hist_ignore_all_dups
setopt hist_ignore_space
setopt hist_find_no_dups
setopt hist_save_no_dups
setopt hist_expire_dups_first
setopt share_history

set-abbreviations() {
  abbr -g p='pacman'
  abbr -g pa='paru'
  abbr -g sp='sudo pacman'
  abbr -g sps='sudo pacman -S'
  abbr -g pqi='pacman -Qi'
  abbr -g psi='pacman -Si'
  abbr -g g='git'
  abbr -g ga='git annex'
  abbr -g v='vim'
  abbr -g ch='chezmoi'
  abbr -g cha='chezmoi add'
  abbr -g chd='chezmoi diff'
  abbr -g che='chezmoi edit'
  abbr -g chap='chezmoi apply'
  abbr -g chg='chezmoi git'
  abbr -g chc='chezmoi cd'
  abbr -g chdx='chezmoi diff -x'
  abbr -g pagp="paru -Gp"
}

# Plugins
zinit depth"1" light-mode for \
  mroth/evalcache

zinit depth"1" wait lucid light-mode for \
  Aloxaf/fzf-tab \
  atinit"ZINIT[COMPINIT_OPTS]=-C; zicompinit; zicdreplay" \
  atload"unalias fsh-alias" zdharma-continuum/fast-syntax-highlighting \
  atload"set-abbreviations; unset -f set-abbreviations" olets/zsh-abbr \
  atload"_zsh_autosuggest_start" zsh-users/zsh-autosuggestions \
  MichaelAquilina/zsh-you-should-use \
  hlissner/zsh-autopair

zinit wait lucid for \
  OMZP::cp \
  OMZP::sudo \
  atload"unalias x" OMZP::extract

# Load completion
zmodload zsh/complist
# Include hidden files
_comp_options+=(globdots)
# Basic auto/tab complete
autoload -Uz compinit && compinit
# Disable zsh menu completion
zstyle ':completion:*' menu no
# Ignore case completion
zstyle ':completion:*' matcher-list '' 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'
# Set description format for fzf-tab group support
zstyle ':completion:*:descriptions' format '[%d]'
# Switch group using `<` and `>`
zstyle ':fzf-tab:*' switch-group '<' '>'
# Disable sort when completing git checkout
zstyle ':completion:*:git-checkout:*' sort false
# Preview directory content with eza when completing cd
zstyle ':fzf-tab:complete:cd:*' fzf-preview 'eza -1 --color=always $realpath'
# Re-add style flags to fzf-tab
zstyle ':fzf-tab:*' use-fzf-default-opts yes
# Set other fzf flags
zstyle ':fzf-tab:*' fzf-flags --scheme=path

# Load in aliases from aliasrc
source "$ZDOTDIR"/aliasrc

# Insulter
source "$ZDOTDIR"/command-not-found.zsh

# Random color script
colorscript random

# fzf tokyonight colors
source ~/.local/share/nvim/lazy/tokyonight.nvim/extras/fzf/tokyonight_night.sh

# git extras completion
source /usr/share/doc/git-extras/git-extras-completion.zsh

# Load zoxide
_evalcache zoxide init zsh

# Starship prompt
_evalcache starship init zsh

# zsh keybindings for fzf
_evalcache fzf --zsh

# thefuck alias
_evalcache thefuck --alias

# Enable transient prompt
source "$ZDOTDIR"/transient-prompt.zsh

