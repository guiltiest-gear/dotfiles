#!/bin/zsh
# If not running interactively, don't do anything
[[ $- != *i* ]] && return

# Remove annoying zinit aliases
declare -A ZINIT=([NO_ALIASES]=1)

# Set up zinit
ZINIT_HOME="${XDG_DATA_HOME:-${HOME}/.local/share}/zinit/zinit.git"
[ ! -d $ZINIT_HOME ] && mkdir -p "$(dirname $ZINIT_HOME)"
[ ! -d $ZINIT_HOME/.git ] && git clone https://github.com/zdharma-continuum/zinit.git "$ZINIT_HOME"
source "${ZINIT_HOME}/zinit.zsh"

# Functions
# NOTE: This is here so that the fzf zsh keybindings work properly after enhanced zsh vi mode init
zvm_after_init() {
  _evalcache fzf --zsh
  # Edit line in vim with ctrl-e
  autoload -Uz edit-command-line
  zle -N edit-command-line
  bindkey '^E' edit-command-line
}

# Use quick escape for getting to normal mode
zvm_config() {
  ZVM_VI_INSERT_ESCAPE_BINDKEY=jk
}

# Trap USR1 signals to trigger a rehash
TRAPUSR1() {
  rehash
}

# Use fd to list path candidates
_fzf_compgen_path() {
  fd --hidden --follow --exclude ".git" . "$1"
}

# Use fd to generate the list for directory completion
_fzf_compgen_dir() {
  fd --type d --hidden --follow --exclude ".git" . "$1"
}

# Enable colors and change prompt:
autoload -U colors && colors

# vi mode
# NOTE: Keep this here in case we don't have zsh-vi-mode installed
bindkey -v
export KEYTIMEOUT=1

## Plugins
# zsh-vi-mode
zinit ice depth=1
# NOTE: For some bizarre reason, this plugin and this alone requires the full
# url to clone without authentication, incredibly bizarre behavior
zinit light https://github.com/jeffreytse/zsh-vi-mode

# fzf-tab
zinit ice depth=1 wait lucid
zinit light Aloxaf/fzf-tab

# zsh-autosuggestions
zinit ice depth=1
zinit light zsh-users/zsh-autosuggestions

# zsh-you-should-use
zinit ice depth=1 wait"1" lucid
zinit light MichaelAquilina/zsh-you-should-use

# evalcache
zinit ice depth=1
zinit light mroth/evalcache

# verbose copy
zinit snippet OMZP::cp

# Prepend sudo
zinit snippet OMZP::sudo

# extract
zinit snippet OMZP::extract

# Exports
export FZF_DEFAULT_OPTS="--no-mouse --cycle --tiebreak=chunk"
export FZF_DEFAULT_COMMAND='rg --files --hidden --smart-case --glob "!.git/*"'
export ZSH_AUTOSUGGEST_STRATEGY=(completion)
# export ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE="fg=#7f7d9c"
export _ZO_EXCLUDE_DIRS="/tmp:/tmp/*:$HOME/Downloads/*:$HOME/Music/*:$HOME/Documents/*:$HOME/Pictures/*:\
$HOME/Videos/*:/run/*:$HOME/.local/share/nvim/*:$HOME/.local/state/nvim/*:$HOME/.local/share/chezmoi/*:\
$HOME/.local/share/chezmoi:$HOME/Downloads:$HOME/Desktop:$HOME/Music:$HOME/Documents:$HOME/Pictures:\
$HOME/Videos:$HOME"
export _ZO_FZF_OPTS="--scheme=path \
  --highlight-line \
  --info=inline-right \
  --ansi \
  --layout=reverse \
  --border=none
  --color=bg+:#283457 \
  --color=bg:#16161e \
  --color=border:#27a1b9 \
  --color=fg:#c0caf5 \
  --color=gutter:#16161e \
  --color=header:#ff9e64 \
  --color=hl+:#2ac3de \
  --color=hl:#2ac3de \
  --color=info:#545c7e \
  --color=marker:#ff007c \
  --color=pointer:#ff007c \
  --color=prompt:#2ac3de \
  --color=query:#c0caf5:regular \
  --color=scrollbar:#27a1b9 \
  --color=separator:#ff9e64 \
  --color=spinner:#ff007c
"
export YSU_MESSAGE_POSITION="after"
export STARSHIP_CONFIG="$XDG_CONFIG_HOME"/starship/starship.toml
export ZSH_EVALCACHE_DIR="$XDG_CACHE_HOME"/zsh/evalcache

# Various options
setopt aliases
setopt complete_aliases
setopt glob_dots
setopt interactive_comments
setopt append_history
setopt hist_ignore_dups
setopt hist_ignore_all_dups
setopt hist_ignore_space
setopt hist_find_no_dups
setopt hist_save_no_dups
setopt hist_expire_dups_first
setopt share_history

# Use vi navigation keys in the menu completion
# bindkey -M menuselect 'h' vi-backward-char
# bindkey -M menuselect 'k' vi-up-line-or-history
# bindkey -M menuselect 'l' vi-forward-char
# bindkey -M menuselect 'j' vi-down-line-or-history

# Load completion
zmodload zsh/complist
# Include hidden files
_comp_options+=(globdots)
# Basic auto/tab complete
autoload -Uz compinit && compinit
# Disable zsh menu completion
zstyle ':completion:*' menu no
# Ignore case completion
zstyle ':completion:*' matcher-list '' 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=*' 'l:|=* r:|=*'
# Set description format for fzf-tab group support
zstyle ':completion:*:descriptions' format '[%d]'
# Switch group using `<` and `>`
zstyle ':fzf-tab:*' switch-group '<' '>'
# Disable sort when completing git checkout
zstyle ':completion:*:git-checkout:*' sort false
# Preview directory content with eza when completing cd
zstyle ':fzf-tab:complete:cd:*' fzf-preview 'eza -1 --color=always $realpath'
# Re-add style flags to fzf-tab
zstyle ':fzf-tab:*' use-fzf-default-opts yes

# Load in aliases from aliasrc
source "$ZDOTDIR"/aliasrc

# Insulter
source "$ZDOTDIR"/command-not-found.zsh

# Random color script
colorscript random

# fzf tokyonight colors
source ~/.local/share/nvim/lazy/tokyonight.nvim/extras/fzf/tokyonight_night.sh

# git extras completion
source /usr/share/doc/git-extras/git-extras-completion.zsh

# Load zoxide
_evalcache zoxide init zsh

# Starship prompt
_evalcache starship init zsh

# Use cdreplay for performance
zinit cdreplay -q

# zsh-syntax-highlighting
# NOTE: Always ensure this is the last plugin sourced
zinit ice depth=1
zinit light zsh-users/zsh-syntax-highlighting
